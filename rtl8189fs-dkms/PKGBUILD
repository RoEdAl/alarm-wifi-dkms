_pkgbase=rtl8189fs
pkgname=rtl8189fs-dkms
pkgver=r37.bf81de8
pkgrel=1
pkgdesc='Kernel module for Realtek RTL8189FTV SDIO wireless devices'
arch=('armv7h' 'aarch64')
url='https://github.com/fifteenhex/xradio.git'
license=('GPL')
depends=('dkms')
makedepends=('git' 'linux-headers')
source=("${_pkgbase}::git+https://github.com/jwrdegoede/rtl8189ES_linux#branch=rtl8189fs"
	'0001-Disable-debug-messages.patch'
	'dkms.conf')
sha256sums=('SKIP'
            '0f9370a2bbd8cccd8cec2aa2c0afe1ae3141879cf1b9dbf7806f7ee282d3478e'
            'd6ad818b6de935095ca992a91326dbceaab20a030fc838d64d1d322042ad3d9a')

pkgver() {
        cd "${srcdir}/${_pkgbase}"
        printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "${srcdir}/${_pkgbase}"

	git apply ../0001-Disable-debug-messages.patch	
}

package() {
	# Copy dkms.conf
	install -Dm644 ${srcdir}/dkms.conf "${pkgdir}/usr/src/${_pkgbase}-${pkgver}/dkms.conf"

	# Set name and version
	sed -e "s/@PKGBASE@/${_pkgbase}/" \
	  -e "s/@PKGVER@/${pkgver}/" \
	  -e "s/@PKGREL@/${pkgrel}/" \
	  -i "${pkgdir}/usr/src/${_pkgbase}-${pkgver}/dkms.conf"

	# Copy sources
	cp -r ${srcdir}/${_pkgbase}/* "${pkgdir}/usr/src/${_pkgbase}-${pkgver}/"
}
