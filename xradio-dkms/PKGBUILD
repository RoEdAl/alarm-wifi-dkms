# Maintainer: Edmunt Pienkowsky <roed@onet.eu>

_pkgbase=xradio
pkgname=xradio-dkms
pkgver=r120.d649e5a
pkgrel=2
pkgdesc='Allwinner xradio driver'
arch=('armv7h')
url='http://github.com/fifteenhex/xradio.git'
license=('GPL')
depends=('dkms' 'xradio-firmware')
makedepends=('git' 'linux-headers')
source=('git+https://github.com/fifteenhex/xradio.git'
	'dkms.conf')
sha256sums=('SKIP'
            '8e48c5a7f31ec17a3123307c8e0e79461c6771baf112dd91bed1b9293e97d044')

pkgver() {
        cd "${srcdir}/xradio"
        printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
	# Copy dkms.conf
	install -Dm644 ${srcdir}/dkms.conf "${pkgdir}/usr/src/${_pkgbase}-${pkgver}/dkms.conf"

	# Set name and version
	sed -e "s/@PKGBASE@/${_pkgbase}/" \
	  -e "s/@PKGVER@/${pkgver}/" \
	  -i "${pkgdir}/usr/src/${_pkgbase}-${pkgver}/dkms.conf"

	# Copy sources
	cp -r ${srcdir}/xradio/* "${pkgdir}/usr/src/${_pkgbase}-${pkgver}/"
}
