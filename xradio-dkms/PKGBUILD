_pkgbase=xradio
pkgname=xradio-dkms
pkgver=r113.33f4b1c
pkgrel=2
pkgdesc='Allwinner xradio driver'
arch=('armv7h')
url='https://github.com/fifteenhex/xradio.git'
license=('GPL')
depends=('dkms')
makedepends=('git' 'linux-headers')
source=('git+https://github.com/fifteenhex/xradio.git'
	'git+https://github.com/armbian/firmware.git'
	'dkms.conf')
sha256sums=('SKIP'
            'SKIP'
            '051dfd6abfd283eb72f1e6c95bc30ff9d1cf37c5227c5c577ab4ab24643d851e')

pkgver() {
        cd "${srcdir}/xradio"
        printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
	# Copy dkms.conf
	install -Dm644 ${srcdir}/dkms.conf "${pkgdir}/usr/src/${_pkgbase}-${pkgver}/dkms.conf"

	# Set name and version
	sed -e "s/@PKGBASE@/${_pkgbase}/" \
	  -e "s/@PKGVER@/${pkgver}/" \
	  -e "s/@PKGREL@/${pkgrel}/" \
	  -i "${pkgdir}/usr/src/${_pkgbase}-${pkgver}/dkms.conf"

	# Copy sources
	cp -r ${srcdir}/xradio/* "${pkgdir}/usr/src/${_pkgbase}-${pkgver}/"

	# Install firmware
	for i in boot fw sdd; do
		install -Dm0644 "${srcdir}/firmware/xr819/${i}_xr819.bin" "${pkgdir}/usr/lib/firmware/xr819/${i}_xr819.bin"
	done
}
